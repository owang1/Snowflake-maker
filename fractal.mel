// UI global variables
global string $sizeSlider;
global string $densitySlider;
global string $angleSlider;

// Initialize other variables
global float $size = 10.0;
global float $constant = 3.5;
global vector $pos = <<0, 0, 0>>;
global int $angle = 45;

// Main function
global proc SnowUI(){
    global string $sizeSlider;
    global string $densitySlider; 
    global string $angleSlider;
    
viewSet -f;
if ( `window -exists MyWindow` ) {
    deleteUI MyWindow; // delete window when it exists
}

window -t "Snowflake Generator" -w 250 -h 300 MyWindow;
 
// Basic layout that places each control in single column 
columnLayout -adjustableColumn true;

    // Insert blank text row for spacing
    text " ";

    // Size slider corresponds to height of 1st polycube    
    $sizeSlider = `floatSliderGrp -columnWidth 1 100
							  -label "Size"
							  -field true
							  -value 10.0
							  -min 8.0
							  -max 12.0`;    
							  
    // Higher density -> less complex snowflake
    $densitySlider = `floatSliderGrp -columnWidth 1 100
							  -label "Density Constant"
							  -field true
							  -value 3.5
							  -min 2.0
							  -max 5.0`;    
							  
    // Angle between each polycube branc
    $angleSlider = `intSliderGrp -columnWidth 1 100
							  -label "Angle"
							  -field true
							  -value 45
							  -min 30
							  -max 72`; 
							  
    // Buttons for generating & deleting snowflakes   
    button -label " Generate Snowflake" -command "Generate";
    button -label " Delete Snowflake" -command "Delete";
							 
showWindow MyWindow;

}
SnowUI();

// Generate() is called when "Generate Snowflake" button is pressed
global proc Generate(){
    global string $sizeSlider;
    global string $densitySlider; 
    global string $angleSlider;
    
    float $size = `floatSliderGrp -q -value $sizeSlider`;
    float $constant = `floatSliderGrp -q -value $densitySlider`;
    int $angle = `intSliderGrp -q -value $angleSlider`;
    vector $pos = <<0, 0, 0>>;
    
    // Create the snowflake
    snowflake($size, $constant, $angle, $pos);
}

global proc Delete(){
    file -f -new;
    viewSet -f;
}


// Recursive Snowflake Generation
global proc snowflake (float $size, float $constant, int $angle, vector $pos) {
    viewSet -f;
    
    // Base case
    if($size < .5){
        return;
    }
    
    for($theta = 0; $theta < 360; $theta += $angle){
        polyCube -d .1 -w .1 -h $size;                // create polycube of height $size
        rotate -r -os -fo 0 0 $theta;                 // move and rotate to correct position
        move -r ($pos.x) ($pos.y) ($pos.z);

        // Divide $size by $constant each time
        vector $next = <<($pos.x + ($size/$constant)*cos(deg_to_rad($theta))), ($pos.y + ($size/$constant)*sin(deg_to_rad($theta))), $pos.z>>;
        snowflake($size/$constant, $constant, $angle, $next);
     } 
}
